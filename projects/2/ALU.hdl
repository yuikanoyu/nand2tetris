// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=x2);
    Not16(in=x2, out=x3);// xの反転
    Mux16(a=x2,b=x3,sel=nx,out=x4);

    Mux16(a=y, b=false, sel=zy, out=y2);
    Not16(in=y2, out=y3);// xの反転
    Mux16(a=y2,b=y3,sel=ny,out=y4);

    Add16(a=x4, b=y4, out=addxy);
    And16(a=x4,b=y4,out=mxy);

    Mux16(a=mxy,b=addxy,sel=f,out=fx);

    Not16(in=fx,out=nfx);

    Mux16(a=fx,b=nfx,sel=no,out[0..7]=out0,out[8..14]= out1,out[15]=out3);

    Or16(a[0..7]=out0,a[8..14]=out1,a[15]=out3,b[0..15]=false,out=out);

    Or8Way(in=out0, out=w1);
    Or8Way(in[0..6]=out1,in[7]=out3, out=w2);
    Or(a=w1, b=w2, out=w3);

    Mux(a=false,b=true,sel=w3,out=zr);

    Or(a=out3,b=false,out=ng);
}